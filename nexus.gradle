apply plugin: 'maven-publish'

def getLocalProperties() {
    Properties localProperties = new Properties()
    File file = project.rootProject.file('local.properties')
    if (file.exists()) {
        localProperties.load(file.newReader("UTF-8"))
    }
    return localProperties
}
// get Local Properties
Properties localProperties = getLocalProperties()

// debug仓库
def SNAPSHOT_REPOSITORY_URL = localProperties.get("nexusRepoSnapshot")
// release仓库
def RELEASE_REPOSITORY_URL = localProperties.get("nexusRepo")
// maven 账户
def REPOSITORY_USER_NAME = localProperties.get("nexusUserName")
def REPOSITORY_USER_PSW = localProperties.get("nexusUserPsw")
def POM_GROUP_ID = project.group.toString()
def POM_ARTIFACT_ID = project.name
def VERSION_NAME = project.version.toString()
def POM_NAME = project.name
def POM_DESCRIPTION = ''
def POM_SCM_URL = ''
def POM_DEVELOPER_ID = localProperties.get("nexusUserName")
def POM_DEVELOPER_NAME = localProperties.get("nexusUserName")
def POM_DEVELOPER_EMAIL = localProperties.get("nexusUserMail")

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release

                groupId = POM_GROUP_ID
                artifactId = POM_ARTIFACT_ID
                version = VERSION_NAME

                pom {
                    name = POM_NAME
                    description = POM_DESCRIPTION
                    url = POM_SCM_URL

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = POM_DEVELOPER_NAME
                            name = POM_DEVELOPER_NAME
                            email = POM_DEVELOPER_EMAIL
                        }
                    }
                    scm {
                        url = POM_SCM_URL
                        connection = ""
                        developerConnection = ""
                    }
                }
            }

        }

        repositories {
            maven {
                url = VERSION_NAME.endsWith('SNAPSHOT') ? SNAPSHOT_REPOSITORY_URL : RELEASE_REPOSITORY_URL
                allowInsecureProtocol true
                credentials {
                    username = REPOSITORY_USER_NAME
                    password = REPOSITORY_USER_PSW
                }
            }
        }
    }
}


